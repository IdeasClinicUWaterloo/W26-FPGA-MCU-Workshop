module video_timing (
    input wire clk,
    input wire reset,
    output reg [11:0] hcount,
    output reg [11:0] vcount,
    output reg hsync,
    output reg vsync,
    output reg de
);

    parameter H_ACTIVE = 1280;
    parameter H_FP     = 110;
    parameter H_SYNC   = 40;
    parameter H_BP     = 220;
    parameter H_TOTAL  = H_ACTIVE + H_FP + H_SYNC + H_BP;

    parameter V_ACTIVE = 720;
    parameter V_FP     = 5;
    parameter V_SYNC   = 5;
    parameter V_BP     = 20;
    parameter V_TOTAL  = V_ACTIVE + V_FP + V_SYNC + V_BP;

    always @(posedge clk) begin
        if (reset) begin
            hcount <= 0;
            vcount <= 0;
        end else begin
            if (hcount == H_TOTAL - 1) begin
                hcount <= 0;
                if (vcount == V_TOTAL - 1)
                    vcount <= 0;
                else
                    vcount <= vcount + 1;
            end else begin
                hcount <= hcount + 1;
            end
        end
    end

    always @(posedge clk) begin
        hsync <= (hcount >= H_ACTIVE + H_FP) &&
                 (hcount <  H_ACTIVE + H_FP + H_SYNC);
        vsync <= (vcount >= V_ACTIVE + V_FP) &&
                 (vcount <  V_ACTIVE + V_FP + V_SYNC);
        de    <= (hcount < H_ACTIVE) && (vcount < V_ACTIVE);
    end

endmodule
